(in-package #:winutil)

(defmacro with-open-clipboard ((&optional (owner '(cffi:null-pointer))) &body body)
  `(progn
     (or (win32:open-clipboard ,owner) (win32-error))
     (unwind-protect (progn ,@body)
       (or (win32:close-clipboard) (win32-error)))))

(defun get-clipboard-text (&optional (owner (cffi:null-pointer)))
  (with-open-clipboard ((hwnd owner))
    (let ((hglb (win32:get-clipboard-data win32:+cf-unicodetext+)))
      (when (cffi:null-pointer-p hglb)
        (return-from get-clipboard-text ""))
      (let ((tstr (win32:global-lock hglb)))
        (check-win32-not-null tstr)
        (unwind-protect (tstring-to-lisp tstr)
          (win32:global-unlock hglb)
          (check-win32-error))))))

(defun set-clipboard-text (string &key (owner (cffi:null-pointer)) (start 0) end)
  (cffi:with-foreign-string ((str size) string :encoding win32:+win32-string-encoding+ :start start :end end)
    (let ((hglb (win32:global-alloc win32:+gmem-moveable+ size))
          success)
      (check-win32-not-null hglb)
      (unwind-protect
           (progn
             (let ((tstr (win32:global-lock hglb)))
               (check-win32-not-null tstr)
               (%memcpy tstr str size))
             (win32:global-unlock hglb)
             (check-win32-error)
             (with-open-clipboard ((hwnd owner))
               (or (win32:empty-clipboard) (win32-error))
               (check-win32-not-null (win32:set-clipboard-data win32:+cf-unicodetext+ hglb)))
             (setf success t))
        (or success (cffi:null-pointer-p (win32:global-free hglb)) (win32-error)))))
  (values))

(defun clear-clipboard (&optional (owner (cffi:null-pointer)))
  (with-open-clipboard ((hwnd owner))
    (or (win32:empty-clipboard) (win32-error)))
  (values))
